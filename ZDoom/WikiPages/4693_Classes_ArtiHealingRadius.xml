<page xmlns="http://www.mediawiki.org/xml/export-0.11/">
  <title>Classes:ArtiHealingRadius</title>
  <ns>0</ns>
  <id>4693</id>
  <revision>
    <id>51449</id>
    <parentid>39509</parentid>
    <timestamp>2024-02-01T15:53:43Z</timestamp>
    <contributor>
      <username>Jekyll Grim Payne</username>
      <id>2175</id>
    </contributor>
    <comment>added zscript definition and See Also</comment>
    <origin>51449</origin>
    <model>wikitext</model>
    <format>text/x-wiki</format>
    <text bytes="3475" sha1="kefpqlsx1wwyl4266r2c7s7vlctbou1" xml:space="preserve">{{Actor|
| type = Artifact
| name = Mystic ambit incant
| class = ArtiHealingRadius
| game = Hexen
| doomednum = 10120
}}[[Classes]]: [[Classes:Inventory|Inventory]]&amp;rarr;&lt;u&gt;ArtiHealingRadius&lt;/u&gt;&lt;br&gt;
The Mystic Ambit Incant is used in Hexen cooperative to have different effects on fellow players, based on the class of the person who picks it up and uses it. All players within 255 map units of the user (including the user themselves) will be affected by the Incant.

The result depends on the value of the user's {{property|Player.HealRadiusType|HealRadiusType}} property:
* &lt;i&gt;Health&lt;/i&gt; restores each affected player's health by 50-90 points.
* &lt;i&gt;Armor&lt;/i&gt; gives each affected player 1 point of [[Classes:HexenArmor|Armor Class]].
* &lt;i&gt;Mana&lt;/i&gt; restores each affected player's [[Classes:Mana1|Blue Mana]] and [[Classes:Mana2|Green Mana]] by 50-99 points. (Each mana type recieves the same amount per Incant.)

=== [[ZScript]] definition ===
{{ZScriptDefinitionNote|actors/hexen/healingradius.zs}}
&lt;syntaxhighlight lang="csharp"&gt;
class ArtiHealingRadius : Inventory
{
	const HEAL_RADIUS_DIST = 255.;

	Default
	{
		+COUNTITEM
		+FLOATBOB
		Inventory.DefMaxAmount;
		+INVENTORY.INVBAR 
		+INVENTORY.FANCYPICKUPSOUND
		Inventory.PickupFlash "PickupFlash";
		Inventory.Icon "ARTIHRAD";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "$TXT_ARTIHEALINGRADIUS";
		Tag "$TAG_ARTIHEALINGRADIUS";
	}
	States
	{
	Spawn:
		HRAD ABCDEFGHIJKLMNOP 4 Bright;
		Loop;
	}	
	
	override bool Use (bool pickup)
	{
		bool effective = false;
		Name mode = 'Health';
		
		PlayerPawn pp = PlayerPawn(Owner);
		if (pp) mode = pp.HealingRadiusType;

		for (int i = 0; i &lt; MAXPLAYERS; ++i)
		{
			if (!playeringame[i])
			{
				continue;
			}

			PlayerPawn mo = players[i].mo;
			if (mo != null &amp;&amp; mo.health &gt; 0 &amp;&amp; mo.Distance2D (Owner) &lt;= HEAL_RADIUS_DIST)
			{
				// Q: Is it worth it to make this selectable as a player property?
				// A: Probably not - but it sure doesn't hurt.
				bool gotsome=false;
				switch (mode)
				{
				case 'Armor':
					for (int j = 0; j &lt; 4; ++j)
					{
						HexenArmor armor = HexenArmor(Spawn("HexenArmor"));
						armor.health = j;
						armor.Amount = 1;
						if (!armor.CallTryPickup (mo))
						{
							armor.Destroy ();
						}
						else
						{
							gotsome = true;
						}
					}
					break;

				case 'Mana':
				{
					int amount = random[HealRadius](50, 99);

					if (mo.GiveAmmo ("Mana1", amount) ||
						mo.GiveAmmo ("Mana2", amount))
					{
						gotsome = true;
					}
					break;
				}

				default:
				//case NAME_Health:
					gotsome = mo.GiveBody (random[HealRadius](50, 99));
					break;
				}
				if (gotsome)
				{
					mo.A_StartSound ("MysticIncant", CHAN_AUTO);
					effective=true;
				}
			}
		}
		return effective;

	}
	
}&lt;/syntaxhighlight&gt;

=== [[DECORATE]] definition ===
{{DecorateDefinitionNote}}
 ACTOR &lt;u&gt;ArtiHealingRadius&lt;/u&gt; : {{Class|Inventory}} native
 {
   +{{Flag|COUNTITEM}}
   +{{Flag|FLOATBOB}}
   {{Property|Inventory.DefMaxAmount}}
   +{{Flag|INVENTORY.INVBAR }}
   +{{Flag|INVENTORY.PICKUPFLASH}}
   +{{Flag|INVENTORY.FANCYPICKUPSOUND}}
   {{Property|Inventory.Icon}} "ARTIHRAD"
   {{Property|Inventory.PickupSound}} "misc/p_pkup"
   {{Property|Inventory.PickupMessage}} "$TXT_ARTIHEALINGRADIUS" // "MYSTIC AMBIT INCANT"
   {{Property|Tag}} "$TAG_ARTIHEALINGRADIUS"
   States
   {
   Spawn:
     HRAD ABCDEFGHIJKLMNOP 4 Bright
     Loop
   }
 }

== See also ==
* [[GiveBody]]
* [[Use]]</text>
    <sha1>kefpqlsx1wwyl4266r2c7s7vlctbou1</sha1>
  </revision>
</page>