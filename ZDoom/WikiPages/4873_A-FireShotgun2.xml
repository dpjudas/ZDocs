<page xmlns="http://www.mediawiki.org/xml/export-0.11/">
  <title>A FireShotgun2</title>
  <ns>0</ns>
  <id>4873</id>
  <revision>
    <id>53774</id>
    <parentid>52909</parentid>
    <timestamp>2024-11-19T10:43:54Z</timestamp>
    <contributor>
      <username>Jekyll Grim Payne</username>
      <id>2175</id>
    </contributor>
    <minor />
    <origin>53774</origin>
    <model>wikitext</model>
    <format>text/x-wiki</format>
    <text bytes="2026" sha1="j8bpem1y9r212fih9ur1xvyzhvuez6m" xml:space="preserve">{{BuiltInFunctionNote|A_FireBullets}}
{{codepointer|Weapon attack}}
{{doomwiki|A_FireShotgun2}}

'''{{class|StateProvider}}'''

{{c|action void '''A_FireShotgun2'''()}}

== Usage ==

Performs Doom's [[Classes:SuperShotgun|super shotgun]] attack, firing 20 pellets dealing 5 * 1d3 damage each. This function is roughly equivalent to [[A_FireBullets]], [[A_StartSound]] and [[A_GunFlash]] with certain predetermined hardcoded parameters:
 SHT2 A 0 [[A_FireBullets]] (11.2, 7.1, 20, 5);
 SHT2 A 0 [[A_StartSound]] ("weapons/sshotf", CHAN_WEAPON);
 SHT2 A 7 [[A_GunFlash]];

[[A_FireShotgun2]] internally uses a somewhat different pitch calculation from [[A_FireBullets]], which results in a different, more oval spread pattern.

== [[ZScript]] definition ==
{{ZScriptDefinitionNote|actors/doom/weaponssg.zs#L66}} 
&lt;syntaxhighlight lang="csharp"&gt;
action void A_FireShotgun2()
{
	if (player == null)
	{
		return;
	}

	A_StartSound ("weapons/sshotf", CHAN_WEAPON);
	Weapon weap = player.ReadyWeapon;
	if (weap != null &amp;&amp; invoker == weap &amp;&amp; stateinfo != null &amp;&amp; stateinfo.mStateType == STATE_Psprite)
	{
		if (!weap.DepleteAmmo (weap.bAltFire, true, 2))
			return;
		
		player.SetPsprite(PSP_FLASH, weap.FindState('Flash'), true);
	}
	player.mo.PlayAttacking2 ();

	double pitch = BulletSlope ();
		
	for (int i = 0 ; i &lt; 20 ; i++)
	{
		int damage = 5 * random[FireSG2](1, 3);
		double ang = angle + Random2[FireSG2]() * (11.25 / 256);

		// Doom adjusts the bullet slope by shifting a random number [-255,255]
		// left 5 places. At 2048 units away, this means the vertical position
		// of the shot can deviate as much as 255 units from nominal. So using
		// some simple trigonometry, that means the vertical angle of the shot
		// can deviate by as many as ~7.097 degrees.

		LineAttack (ang, PLAYERMISSILERANGE, pitch + Random2[FireSG2]() * (7.097 / 256), damage, 'Hitscan', "BulletPuff");
	}
}&lt;/syntaxhighlight&gt;

== See Also ==
* [[A_FireBullets]]
* [[LineAttack (ZScript)]]
* {{class|SuperShotgun}}

[[Category:StateProvider]]</text>
    <sha1>j8bpem1y9r212fih9ur1xvyzhvuez6m</sha1>
  </revision>
</page>